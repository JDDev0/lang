lang.name = Function Pointer
lang.version = v1.0.0

# Function Pointer
# Create function
# fp.name = ($var) -> { # Function head: Parameter definition
#     [CODE...] # Function body: Code execution
# } # Function end
# Call function:
# fp.name($var)

# Create:
{ # Normal
	# Without vars:
	# fp.name = () -> {
	fp.test = () -> {
		func.println(This is a Test!)
		func.println(Ok...)
	}
	fp.test()
	
	func.println(-----------------)
	
	# With vars:
	#             Var    Array  Function pointer
	# fp.name = ($var1,  &var2,     fp.var3) -> {
	fp.printRandom = ($min, $max) -> {
		$rand = func.addi(func.modi(func.rand(), func.addi(func.subi($max, $min), 1)), $min)
		func.println(Random number: $rand!)
	}
	fp.printRandom(42, 42)
	fp.printRandom(41, 42)
	fp.printRandom(0, 42)
	
	func.println(-----------------)
}
{ # One-Line
	# fp.name = () -> [CODE...]
	fp.printNormal = ($text) -> func.println($text)
	fp.printNormal(100 - TESTed!)
	
	func.println(-----------------)
}

# Return:
fp.testRet = () -> {
	func.println(I was executed!)
	return # Will return -> Everything after won't be executed
	func.println(Hello?)
}
fp.testRet()

func.println(-----------------)

fp.getRandom = () -> return func.rand() # Will return a value to the function caller
$ran = fp.getRandom()
func.println($ran)
func.println(fp.getRandom())

func.println(-----------------)

# Function Pointer as return value
fp.func = () -> return YES!
fp.get = () -> {
	return fp.func
}
$g = fp.get()
func.println($g)
$g = fp.get()()
func.println($g)

func.println(-----------------)

# Call returned function pointer inside another function call
func.println(fp.get())
func.println(fp.get()())

func.println(-----------------)

# Copy function pointer
# Function Pointer
# fp.name = fp.name
fp.orig = () -> {
	func.println(A function)
}
fp.orig() # Prints: "A function"
fp.copy = fp.orig # Copying
fp.copy() # Prints: "A function"
# Changing original function
fp.orig = () -> func.println(Another function)
fp.orig() # Prints: "A other function"
fp.copy() # Still prints "A function"

func.println(-----------------)

# Predifined function
# fp.name = func.name
fp.pt = func.printTerminal
fp.pt(-1, Function Pointer!)
func.printTerminal(-1, Function Pointer!)

func.println(-----------------)

# Function pointer argument duplication
# If not enough arguments are provided, the last given argument will be copy
# If no argument was given, all variables will be empty
# If too many arguments are given, the arguments after the last parameter will be ignored
fp.manyArgs = ($a, $b, $c, $d, $e, $f, $g) -> {
	func.println(|$a  $b  $c  $d  $e  $f  $g|)
}
fp.manyArgs()
fp.manyArgs(A)
fp.manyArgs(A, B)
fp.manyArgs(A, B, C)
fp.manyArgs(A, B, C, D)
fp.manyArgs(A, B, C, D, E)
fp.manyArgs(A, B, C, D, E, F)
fp.manyArgs(A, B, C, D, E, F, G)
fp.manyArgs(A, B, C, D, E, F, G, H)
fp.manyArgs(A, B, C, D, E, F, G, H, I)

func.println(-----------------)

# Varargs
{
	# The last parameter can be a vargargs parameter ($<name>... [Text varargs] or &<name>... [Array varargs])
	
	# Text varargs
	# Everything after the argument prior to the last argument will be combined as text and written to $args
	fp.testTextVarArgs = ($num, $args...) -> {
		func.printTerminal(-1, \nFrom "fp.testTextVarArgs")
		func.println(\$num: $num)
		func.println(\$args: $args)
		func.printError()
	}
	fp.testTextVarArgs()
	fp.testTextVarArgs(1)
	fp.testTextVarArgs(1, 2)
	fp.testTextVarArgs(1, 2, 3)
	fp.testTextVarArgs(1, 2, 3, 4)
	fp.testTextVarArgs(1, 2, 3, 4, 5)
	fp.testTextVarArgs(1, 2, 3, 4, 5, 6)
	
	func.println(-----------------)
	
	# Array varargs
	# Everything after the argument prior to the last argument will be combined as Array and written to &args
	fp.testArrayVarArgs = ($num, &args...) -> {
		func.printTerminal(-1, \nFrom "fp.testArrayVarArgs")
		func.println(\$num: $num)
		func.println(\&args: func.arrayGetAll(&args))
		func.printError()
		func.printTerminal(-1, length(\&args): func.arrayLength(&args))
		func.printError()
	}
	fp.testArrayVarArgs()
	fp.testArrayVarArgs(1)
	fp.testArrayVarArgs(1, 2)
	fp.testArrayVarArgs(1, 2, 3)
	fp.testArrayVarArgs(1, 2, 3, 4)
	fp.testArrayVarArgs(1, 2, 3, 4, 5)
	fp.testArrayVarArgs(1, 2, 3, 4, 5, 6)
	
	func.println(-----------------)
}

# Every value at function call will be copied to function pointer:
# Changed values
{
	$a = 42
	fp.mod = ($a) -> {
		$a = 43
		func.println(Local: $a)
	}
	fp.mod($a)
	fp.mod = () -> {
		$a = 44
		func.println(Inner: $a)
	}
	fp.mod()
	func.println(Outer: $a)
	
	# If you want to copy a local variable to the caller: See [07_loops.lang: func.copyAfterFP]
	
	func.println(-----------------)
}

# Use values that are defined after function pointer definition
{
	fp.printNot = () -> {
		func.println($not)
	}
	
	fp.printNot() # Will print "$not"
	$not = I'm defined!
	fp.printNot() # Will print "I'm defined!"
	
	func.println(-----------------)
}

# Returning of assignment
{	
	$tmp = 0
	$val = 42
	fp.genNextNumber = () -> {
	    func.copyAfterFP($[tmp], $[tmp]) # See [07_loops.lang: func.copyAfterFP]
		return $tmp = func.addi($val, $tmp) # Not supported: Empty assignment / translation and translations
	}
	func.println(fp.genNextNumber())
	func.println(fp.genNextNumber())
	func.println(fp.genNextNumber())
	
	func.println(-----------------)
	
	&a = func.arrayOf(6, 8, 9, 4, 2, 0, 1, 5, 3, 7)
	$tmpIndex = 0
	fp.next = (&arr) -> {
		func.copyAfterFP($[tmpIndex], $[tmpIndex]) # See [07_loops.lang: func.copyAfterFP]
		return $tmpIndex = func.arrayGet(&arr, $tmpIndex)
	}
	func.println(fp.next(&a))
	func.println(fp.next(&a))
	func.println(fp.next(&a))
	
	func.println(-----------------)
}

# Set Function Pointer to a returned value
{
	fp.a = func.printError
	fp.a(0-ABC)
	fp.b = () -> return fp.a
	fp.b()(1-ABC)
	fp.c = fp.a
	fp.c(2-ABC)
	fp.d = fp.b()
	fp.d(3-ABC)
	fp.e = func.randChoice(fp.a, fp.c)
	fp.e(4-ABC)
	fp.f = func.randChoice(func.println, func.errorln)
	fp.f(5-ABC)
	fp.g = func.exec(ret\!urn func\!.printError)
	fp.g(6-ABC)
	fp.h = func.exec(ret\!urn (\$text...) -> {\nfunc\!.printError(\$text)\n})
	fp.h(7-ABC)
	fp.i = func.exec({{{
		return ($text...) -> {
			func.printError($text)
		}
	}}})
	fp.i(8-ABC)
	
	func.println(-----------------)
}

# Anonymous Function Pointer as return value
{
	fp.a = () -> {
		return ($text...) -> {
			func.println($text)
		}
	}
	fp.a()(abc, def)
	fp.b = fp.a()
	fp.b(def, abc)
	
	func.println(-----------------)
}

# Prevent call of returned function pointer
{
	fp.getABCFunc = () -> {
		return () -> {
			return abc
		}
	}
	func.println(fp.getABCFunc()())
	func.println(fp.getABCFunc()\!()) # Prevent call of returned function pointer
	
	func.println(-----------------)
}

# Call of returned function pointer is not possible inside CONDITIONs
{
	fp.fp = () -> {
		func.println(fp.fp was called!)
		return 1
	}
	fp.retFP = () -> {
		func.println(fp.retFP was called!)
		return fp.fp
	}
	
	# Call of returned function pointer is not possible inside CONDITIONs in:
	# con.if(...), con.elif(...), parser.con(...) [See: 06_if.lang]
	
	$bool = parser.con(fp.retFP()()) # Would not call returned function pointer
	func.println($bool)
	
	func.println(-----------------)
	
	$ret = fp.retFP()() # Returned function pointer must be called outside of a CONDITION
	func.println(parser.con($ret))
	
	func.println(-----------------)
}

# An other possibility for "returning" a value
{
	# With call by value
	{
		$a = 42
		fp.changeValue = ($ptr) -> {
			$*ptr = changed value # Pointer dereferencing
		}
		func.println($a)
		fp.changeValue($[a]) # Provide pointer to "$a"
		func.println($a)
	}
	
	func.println(-----------------)
	
	# With call by pointer
	{
		$a = 42
		fp.changeValue = ($[ptr]) -> {
			$*ptr = changed value # Pointer dereferencing
		}
		func.println(5, $a)
		fp.changeValue($a) # A pointer will be created for "$a" during the call process
		func.println(5, $a)
	}
	
	func.println(-----------------)
}

# End the program early & return DataObject
func.println(Will be executed)
return 42.42 # A return statement outside of a function pointer function body will end the program execution early
func.println(Won't be executed)
