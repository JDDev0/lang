lang.name = Variable Scope
lang.version = v0.2.0

$a = test # "Global Scope Variable"
func.printTerminal(-1, Global scope)
func.printTerminal(-1, \$a: $a)
{
	func.printTerminal(-1, \s)
	func.printTerminal(-1, Global scope ["Block"])
	func.printTerminal(-1, \$a: $a)
	$b = test2 # Also "Global Scope Variable", because "{"-"Block" is no block
	func.printTerminal(-1, \$b: $b)
}
func.printTerminal(-1, \s)
func.printTerminal(-1, Global scope)
func.printTerminal(-1, \$b: $b)

fp.test = ($d) -> { # "Function Scope Variable"
	func.printTerminal(-1, \s)
	func.printTerminal(-1, Function scope [fp.test])
	func.printTerminal(-1, \$d: $d)
	func.printTerminal(-1, \$a: $a)
	func.printTerminal(-1, \$b: $b)
	$a = overriden # "Function Scope Variable"
	               # {"Global Scope Variable" $a will be shadowed}
	func.printTerminal(-1, \$a: $a)
	func.printTerminal(-1, \$c: $c)
	$c = new # "Function Scope Variable" {"Global Scope Variable" $c will be shadowed}
	func.printTerminal(-1, \$c: $c)
	
	fp.innerTest = () -> {
		func.printTerminal(-1, \s)
		func.printTerminal(-1, Function scope [fp.test [fp.innerTest]])
		func.printTerminal(-1, \$a: $a)
		func.printTerminal(-1, \$b: $b)
		func.printTerminal(-1, \$c: $c)
		func.printTerminal(-1, \$d: $d)
		$a = new new var # "(Inner) Function Scope Variable" {$a will be shadowed}
		func.printTerminal(-1, \$a: $a)
		
		func.copyAfterFP($[a], $[a]) # Will copy $a to the callers variable scope [After function]
	}
	fp.innerTest()
	
	func.printTerminal(-1, \s)
	func.printTerminal(-1, Function scope [fp.test])
	func.printTerminal(-1, \$a: $a)
	func.copyAfterFP($[a], $[a]) # Will copy $a to the callers variable scope [After function]
}

fp.test(Tested)
func.printTerminal(-1, \s)
func.printTerminal(-1, Global scope)
func.printTerminal(-1, \$a: $a)

$a = test # "Global Scope Variable"
$c = var test # "Global Scope Variable"
fp.test(Tested) # All varibales of the scope of the function call will be copied
func.printTerminal(-1, \s)
func.printTerminal(-1, Global scope)
func.printTerminal(-1, \$a: $a)

con.if(1)
	func.printTerminal(-1, \s)
	func.printTerminal(-1, Global scope ["IF"])
	$e = test
	func.printTerminal(-1, $e)
con.endif

func.printTerminal(-1, \s)
func.printTerminal(-1, Global scope)
func.printTerminal(-1, $e)

# Translations will allways be in the global scope
fp.a = () -> {
	fp.b = () -> {
		fp.c = () -> {
			fp.d = () -> {
				fp.e = () -> {
					fp.f = () -> {
						global.translation = A translation
					}
					fp.f()
				}
				fp.e()
			}
			fp.d()
		}
		fp.c()
	}
	fp.b()
}
fp.a()

func.printTerminal(-1, \s)
func.printTerminal(-1, global.translation = func.getLangRequest(global.translation))
