# Lang data
lang.name = GuessANumber
lang.version = v1.0.0

$end = 0
$low = 1
$high = 10
fp.gameLoop = () -> {
	$win = 0
	$guesses = 0
	
	# Generate random number between $low and $high
	$num = func.addi(func.modi(func.rand(), func.addi(func.subi($high, $low), 1)), $low)
	fp.game = () -> {
		$in = func.readTerminal(Gues a number between $low and $high!)
		con.if($in < $num)
			func.printTerminal(1, The number is heigher than your input!)
		con.elif($in > $num)
			func.printTerminal(1, The number is lower than your input!)
		con.elif($in == $num)
			$win = 1
			func.copyAfterFP($[win], $[win])
		con.else
			func.printTerminal(5, Invalid Input!)
		con.endif
		
		$guesses = func.addi($guesses, 1)
		func.copyAfterFP($[guesses], $[guesses])
	}
	fp.gameWin = () -> return $win
	func.repeatUntil(fp.game, fp.gameWin)
	
	func.printTerminal(1, You have won the game after $guesses guesses!)
	$in = func.readTerminal(Type YES, if you'd like to play again!)
	con.if($in != YES)
		$end = 1
		func.copyAfterFP($[end], $[end])
		func.printTerminal(2, Thanks for playing!)
	con.endif
}
fp.gameEnd = () -> return $end
func.repeatUntil(fp.gameLoop, fp.gameEnd)
